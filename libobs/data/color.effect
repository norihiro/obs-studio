float srgb_linear_to_nonlinear_channel(float u)
{
	return (u <= 0.0031308) ? (12.92 * u) : ((1.055 * pow(u, 1. / 2.4)) - 0.055);
}

float3 srgb_linear_to_nonlinear(float3 v)
{
	return float3(srgb_linear_to_nonlinear_channel(v.r), srgb_linear_to_nonlinear_channel(v.g), srgb_linear_to_nonlinear_channel(v.b));
}

float srgb_nonlinear_to_linear_channel(float u)
{
	return (u <= 0.04045) ? (u / 12.92) : pow((u + 0.055) / 1.055, 2.4);
}

float3 srgb_nonlinear_to_linear(float3 v)
{
	return float3(srgb_nonlinear_to_linear_channel(v.r), srgb_nonlinear_to_linear_channel(v.g), srgb_nonlinear_to_linear_channel(v.b));
}

float3 rec709_to_rec2020(float3 v)
{
	float r = dot(v, float3(0.62740389593469914, 0.32928303837788397, 0.043313065687417190));
	float g = dot(v, float3(0.069097289358232047, 0.91954039507545904, 0.011362315566309173));
	float b = dot(v, float3(0.016391438875150235, 0.088013307877225860, 0.89559525324762468));
	return float3(r, g, b);
}

float3 rec2020_to_rec709(float3 v)
{
	float r = dot(v, float3(1.6604910021084343, -0.58764113878854973, -0.072849863319884745));
	float g = dot(v, float3(-0.12455047452159063, 1.1328998971259603, -0.0083494226043695080));
	float b = dot(v, float3(-0.018150763354905199, -0.10057889800800746, 1.1187296613629123));
	return float3(r, g, b);
}

float reinhard_channel(float x)
{
	return x / (x + 1.);
}

float3 reinhard(float3 rgb)
{
	return float3(reinhard_channel(rgb.r), reinhard_channel(rgb.g), reinhard_channel(rgb.b));
}

float linear_to_st2084_channel(float x)
{
	float c = pow(abs(x), 0.1593017578);
	return pow((0.8359375 + 18.8515625 * c) / (1. + 18.6875 * c), 78.84375);
}

float3 linear_to_st2084(float3 rgb)
{
	return float3(linear_to_st2084_channel(rgb.r), linear_to_st2084_channel(rgb.g), linear_to_st2084_channel(rgb.b));
}

float st2084_to_linear_channel(float u)
{
	float c = pow(abs(u), 1. / 78.84375);
	return pow(abs(max(c - 0.8359375, 0.) / (18.8515625 - 18.6875 * c)), 1. / 0.1593017578);
}

float3 st2084_to_linear(float3 v)
{
	return float3(st2084_to_linear_channel(v.r), st2084_to_linear_channel(v.g), st2084_to_linear_channel(v.b));
}

float linear_to_hlg_channel(float u)
{
	float ln2_i = 1. / log(2.);
	float m = 0.17883277 / ln2_i;
	return (u <= (1. / 12.)) ? sqrt(3. * u) : ((log2((12. * u) - 0.28466892) * m) + 0.55991073);
}

float eetf_0_1000(float Lw, float maxRGB1_pq)
{
	float Lw_pq = linear_to_st2084_channel(Lw / 10000.);
	float E1 = maxRGB1_pq / Lw_pq;
	float maxLum = linear_to_st2084_channel(.1) / Lw_pq;
	float KS = (1.5 * maxLum) - 0.5;
	float E2 = E1;
	if (E1 > KS)
	{
		float T = (E1 - KS) / (1. - KS);
		float Tsquared = T * T;
		float Tcubed = Tsquared * T;
		float P = (2. * Tcubed - 3. * Tsquared + 1.) * KS + (Tcubed - 2. * Tsquared + T) * (1. - KS) + (-2. * Tcubed + 3. * Tsquared) * maxLum;
		E2 = P;
	}
	float E3 = E2;
	float E4 = E3 * Lw_pq;
	return E4;
}

float3 maxRGB_eetf(float3 rgb, float Lw, float Lmax)
{
	float maxRGB_linear = max(max(rgb.r, rgb.g), rgb.b);
	float maxRGB1_pq = linear_to_st2084_channel(maxRGB_linear);
	float maxRGB2_pq = eetf_0_1000(Lw, maxRGB1_pq);
	float maxRGB2_linear = st2084_to_linear_channel(maxRGB2_pq);

	// avoid divide-by-zero possibility
	maxRGB_linear = max(6.10352e-5, maxRGB_linear);

	rgb *= maxRGB2_linear / maxRGB_linear;
	return rgb;
}

float3 linear_to_hlg(float3 rgb, float Lw)
{
	rgb = saturate(rgb);

	if (Lw > 1000.)
	{
		rgb = maxRGB_eetf(rgb, Lw, 1000.);
		rgb *= 10000. / Lw;
	}
	else
	{
		rgb *= 10.;
	}

	float Yd = dot(rgb, float3(0.2627, 0.678, 0.0593));

	// pow(0., exponent) can lead to NaN, use smallest positive normal number
	Yd = max(6.10352e-5, Yd);

	rgb *= pow(Yd, -1. / 6.);
	return float3(linear_to_hlg_channel(rgb.r), linear_to_hlg_channel(rgb.g), linear_to_hlg_channel(rgb.b));
}

float hlg_to_linear_channel(float u)
{
	float ln2_i = 1. / log(2.);
	float m = ln2_i / 0.17883277;
	float a = -ln2_i * 0.55991073 / 0.17883277;
	return (u <= 0.5) ? ((u * u) / 3.) : ((exp2(u * m + a) + 0.28466892) / 12.);
}

float3 hlg_to_linear(float3 v, float exponent)
{
	float3 rgb = float3(hlg_to_linear_channel(v.r), hlg_to_linear_channel(v.g), hlg_to_linear_channel(v.b));
	float Ys = dot(rgb, float3(0.2627, 0.678, 0.0593));
	rgb *= pow(Ys, exponent);
	return rgb;
}
